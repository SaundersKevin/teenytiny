# SET acts like "#define" in C. This has to be placed at the very top of your .tiny file
SET evenNumber 2
SET oddNumber 1

# Removes the stack from the program file
STACK 0

# Printing statements is handled by PRINT string
PRINT "Enter a number:"

# Taking input is handled by INPUT variable_to_assign. This will only take a number as input
INPUT num

PRINT "Here is the collatz sequence for your number:"

# IF statements have to be ended with a ENDIF
IF num <= 0
	GOTO end
ENDIF

# Since functions are not supported, LABEL and GOTO are used
LABEL check

# PRINT can also print the value of variables
PRINT num

IF num == 1
	GOTO end
ENDIF

# LET is used to assign variables to a value
# if the variable doesn't exist it is created
LET a = num

# WHILE and IF are the only loops
WHILE a > 0

	IF a == evenNumber
		GOTO even
	ENDIF

	IF a == oddNumber
		GOTO odd
	ENDIF

	LET a = a - 2

ENDWHILE

LABEL even
	LET num = num / 2
	GOTO check

LABEL odd
	LET num = 3 * num
	LET num = num + 1
	GOTO check

LABEL end
